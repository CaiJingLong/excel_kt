import com.vanniktech.maven.publish.SonatypeHost

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.10'
    id "com.vanniktech.maven.publish" version "0.22.0"
}

group = 'top.kikt'
version = '1.3.0'
//version = '1.2.2-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    // add simple logger
    def slf4jVersion = '2.0.5'
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
//    testImplementation "org.slf4j:slf4j-simple:$slf4jVersion"
    testImplementation 'org.apache.logging.log4j:log4j-core:2.19.0'
    testImplementation 'ch.qos.logback:logback-classic:1.4.5'

    // excel 相关的库 apache的poi库
    // https://mvnrepository.com/artifact/org.apache.poi/poi
    implementation group: 'org.apache.poi', name: 'poi', version: '5.2.2'
    // https://mvnrepository.com/artifact/org.apache.poi/poi-ooxml
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.2.2'
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

// publish
mavenPublishing {

    publishToMavenCentral(SonatypeHost.S01, true)
    signAllPublications()

    def repoName = "excel"
    def desc = "Excel utils for wrapper apache poi."

    def licenseUrl = "http://www.apache.org/licenses/LICENSE-2.0.txt"

    def author = "CaiJingLong"
    def repoUrl = "https://github.com/CaiJingLong/excel_kt/"
    def gitUrl = "${repoUrl}.git"

    pom {
        name = repoName
        description = desc
        inceptionYear = "2022"
        url = repoUrl
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = licenseUrl
                distribution = licenseUrl
            }
        }
        developers {
            developer {
                id = author
                name = author
                url = "https://github.com/$author/"
            }
        }
        scm {
            url = repoUrl
            connection = gitUrl
            developerConnection = gitUrl
        }
    }
}

publishing {
    repositories {
        maven {
            name = "BuildDir"
            url = "$buildDir/repoLocal"
        }
    }
}